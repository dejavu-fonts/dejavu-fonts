# $Id$

# script file for FontForge for font narrowing
# usage: fontforge -script narrow.pe scale *.sfd
#   created files have suffix .narrow

# DejaVu Condensed have scale 90

# FIX: fill in the right version after worthless.patch is applied to FF
#if ($version < "20041012")
#  Error("Your version of FontForge transforms glyphs w/o content");
#endif
if ($argc < 2)
  Error("usage: scale sfds...")
endif
scale = Strtol($argv[1])
i = 2
while (i < $argc)
  Open($argv[i], 1)
  SelectAll()
  Scale(scale, 100)
  # Panose changes are valid only for DejaVu fonts (I guess)
  SetPanose(3, 6)
  new_fontname = $fontname
  j = Strstr(new_fontname, "-Roman")
  if (j > -1)
    new_fontname = Strsub(new_fontname, 0, j)
  endif
  j = Strstr(new_fontname, "-")
  if (j > -1)
    new_fontname = Strsub(new_fontname, 0, j) + "Condensed" + Strsub(new_fontname, j)
  else
    new_fontname = new_fontname + "Condensed"
  endif
  new_fullname = $fullname
  j = Strstr(new_fullname, "Bold")
  if (j == -1)
    j = Strstr(new_fullname, "Oblique")
  endif
  if (j > -1)
    new_fullname = Strsub(new_fullname, 0, j) + "Condensed " + Strsub(new_fullname, j)
  else
    new_fullname = new_fullname + " Condensed"
  endif
  new_familyname = $familyname + " Condensed"
  SetFontNames(new_fontname, new_familyname, new_fullname)
  SetTTFName(1033, 3, new_fullname)
  Save($argv[i] + ".narrow")
  i++
endloop
