#!/usr/bin/env fontforge
# $Id$

# script file for FontForge for TTF generation
# usage:
#   mkdir generated
#   chmod +x generate.pe
#   ./generate.pe *.sfd

# font generation flags:
#      8 => do not include TT instructions (for experimental typefaces)
#   0x20 => generate a 'PfEd' table and store glyph comments
#   0x40 => generate a 'PfEd' table and store glyph colors
#   0x800 => generate old fashioned kern tables.
#           - this one is important because it generates correct kerning tables
def_gen_flags = 0x20 + 0x40 + 0x800
exp_gen_flags = def_gen_flags + 8

if ($version < "20070501")
  Error("Your version of FontForge is too old - 20070501 or newer is required");
endif
# FoundryName is not used in TTF generation
SetPref("FoundryName", "DejaVu")
# first 4 characters of TTFFoundry are used for achVendId
SetPref("TTFFoundry", "DejaVu")
i = 1
while ( i < $argc )
  Open($argv[i], 1)
  gen_flags = def_gen_flags
  # Serif Oblique and Serif Bold Oblique are experimental
  if ((Strcasestr ($fontname, "Serif") > -1) && (Strcasestr ($fontname, "Oblique") > -1))
    gen_flags = exp_gen_flags
  endif
  if (Strcasestr ($fontname, "Condensed") > -1)
    gen_flags = exp_gen_flags
  endif
  if (Strcasestr ($fontname, "ExtraLight") > -1)
    gen_flags = exp_gen_flags
  endif
  # no non-combining ligatures in Mono (we remove all because there are no valid combining ligatures right now)
  if (Strcasestr ($fontname, "Mono") > -1)
    # do not remove ligatures from Arabic
    Select("U+0600", "U+06FF", "U+FB50", "U+FDFF", "U+FE70", "U+FEFF")
    SelectInvert()
    RemoveLookup("'liga' Standard Ligatures in Latin lookup 8")
    SelectNone()
  endif
  Generate("generated/" + $fontname + ".ttf", "", gen_flags)
  i++
endloop
